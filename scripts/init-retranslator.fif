#!/usr/bin/fift -s
"TonUtil.fif" include
"Asm.fif" include

"retranslator-code.fif" include =: contract_code

"wallet-retranslator.pk" load-generate-keypair 
=: privkey
=: pubkey
<b 0 16 u, 0 32 u, pubkey B, contract_code .s ref, b> =: contract_storage

<b b{00110} s, contract_code ref, contract_storage ref, b>
dup =: state_init

dup hashu 0 swap 2constant contract_address

."new retranslator wallet address = " contract_address .addr cr

."Non-bounceable address (for init): " contract_address 7 .Addr cr
."Bounceable address (for later access): " contract_address 6 .Addr cr

<b 
  0 32 u, now 40 + 32 u, 0 32 u,
  255 8 u, // mode = retranslate
  1 8 u,   // one chain
  20000 16 u, // hops
  5 8 u,     // split hops
  99 Gram* Gram, // first message size
  30000 16 u,   // preference
b> =: init_message


init_message ."signing message: " <s csr. cr
init_message hashu privkey ed25519_sign_uint =: signature
<b b{1000100} s, contract_address addr, b{000010} s, state_init <s s, b{0} s, signature B, init_message <s s, b>
dup ."External message for initialization is " <s csr. cr
2 boc+>B dup Bx. cr
"query-init-retranslator.boc" tuck B>file
."(Saved retranslator contract creating query to file " type .")" cr